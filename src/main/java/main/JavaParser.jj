/**
* @author Zach Jones (zachtjones16@gmail.com)
* This file generates the parser, using JavaCC
* Some parts of this file are taken from the examples in JavaCC documentation.
*/

options {
    // instances so we can have multiple at same time
    STATIC = false;
}

PARSER_BEGIN(JavaParser)
package main;

import java.io.FileNotFoundException;
import java.io.FileInputStream;
import tree.*;
import java.util.ArrayList;
import java.nio.charset.Charset;
import helper.*;

// hide the warnings from dead code -- there's no way to block it
@SuppressWarnings("unused")
public class JavaParser {

    public String fileName;

    public static CompilationUnit parse(String file) throws FileNotFoundException, ParseException {
        JavaParser parser = new JavaParser(new FileInputStream(file), Charset.defaultCharset());
        parser.fileName = file;
        return parser.CompilationUnit(file);
    }

}

PARSER_END(JavaParser)

/* Now specify the lexer commands */

SKIP : /* WHITE SPACE */
{
  " " | "\t" | "\n" | "\r" | "\f"
}

SPECIAL_TOKEN : /* Comments and Annotations */
{
  <LINE_COMMENT: "//" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
| <BLOCK_COMMENT: "/*" (~["*"])* "*" ("*" | (~["*","/"] (~["*"])* "*"))* "/">
| <ANNOTATION: "@" (~["\n","\r"])* ("\n"|"\r"|"\r\n")>
}

TOKEN : /* RESERVED WORDS AND LITERALS */
{
  < ABSTRACT: "abstract" >
| < BOOLEAN: "boolean" >
| < BREAK: "break" >
| < BYTE: "byte" >
| < CASE: "case" >
| < CATCH: "catch" >
| < CHAR: "char" >
| < CLASS: "class" >
| < CONST: "const" >
| < CONTINUE: "continue" >
| < _DEFAULT: "default" >
| < DO: "do" >
| < DOUBLE: "double" >
| < ELSE: "else" >
| < ENUM: "enum" >
| < EXTENDS: "extends" >
| < FALSE: "false" >
| < FINAL: "final" >
| < FINALLY: "finally" >
| < FLOAT: "float" >
| < FOR: "for" >
| < GOTO: "goto" >
| < IF: "if" >
| < IMPLEMENTS: "implements" >
| < IMPORT: "import" >
| < INSTANCEOF: "instanceof" >
| < INT: "int" >
| < INTERFACE: "interface" >
| < LONG: "long" >
| < NATIVE: "native" >
| < NEW: "new" >
| < NULL: "null" >
| < PACKAGE: "package">
| < PRIVATE: "private" >
| < PROTECTED: "protected" >
| < PUBLIC: "public" >
| < RETURN: "return" >
| < SHORT: "short" >
| < STATIC: "static" >
| < SUPER: "super" >
| < SWITCH: "switch" >
| < SYNCHRONIZED: "synchronized" >
| < THIS: "this" >
| < THROW: "throw" >
| < THROWS: "throws" >
| < TRANSIENT: "transient" >
| < TRUE: "true" >
| < TRY: "try" >
| < VOID: "void" >
| < VOLATILE: "volatile" >
| < WHILE: "while" >
}

TOKEN : /* LITERALS */
{
  < INTEGER_LITERAL:
        <DECIMAL_LITERAL> (["l","L"])?
      | <HEX_LITERAL> (["l","L"])?
      | <OCTAL_LITERAL> (["l","L"])?
  >
|
  < #DECIMAL_LITERAL: ["1"-"9"] (["0"-"9"])* >
|
  < #HEX_LITERAL: "0" ["x","X"] (["0"-"9","a"-"f","A"-"F"])+ >
|
  < #OCTAL_LITERAL: "0" (["0"-"7"])* >
|
  < FLOATING_POINT_LITERAL:
        (["0"-"9"])+ "." (["0"-"9"])* (<EXPONENT>)? (["f","F","d","D"])?
      | "." (["0"-"9"])+ (<EXPONENT>)? (["f","F","d","D"])?
      | (["0"-"9"])+ <EXPONENT> (["f","F","d","D"])?
      | (["0"-"9"])+ (<EXPONENT>)? ["f","F","d","D"]
  >
|
  < #EXPONENT: ["e","E"] (["+","-"])? (["0"-"9"])+ >
|
  < CHARACTER_LITERAL:
      "'"
      (   (~["'","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )
      "'"
  >
|
  < STRING_LITERAL:
      "\""
      (   (~["\"","\\","\n","\r"])
        | ("\\"
            ( ["n","t","b","r","f","\\","'","\""]
            | ["0"-"7"] ( ["0"-"7"] )?
            | ["0"-"3"] ["0"-"7"] ["0"-"7"]
            )
          )
      )*
      "\""
  >
}

TOKEN : /* IDENTIFIERS */
{
    < IDENTIFIER: <LETTER> (<LETTER>|<DIGIT>)* >
|   < #LETTER: [ "A"-"Z", "a"-"z", "_" ] >
|   < #DIGIT: [ "0"-"9" ] >
}

TOKEN : /* SEPARATORS */
{
  < LPAREN: "(" >
| < RPAREN: ")" >
| < LBRACE: "{" >
| < RBRACE: "}" >
| < LBRACKET: "[" >
| < RBRACKET: "]" >
| < SEMICOLON: ";" >
| < COMMA: "," >
| < DOT: "." >
}

TOKEN : /* OPERATORS */
{
  < ASSIGN: "=" >
| < GT: ">" >
| < LT: "<" >
| < BANG: "!" >
| < TILDE: "~" >
| < HOOK: "?" >
| < COLON: ":" >
| < EQ: "==" >
| < LE: "<=" >
| < GE: ">=" >
| < NE: "!=" >
| < SC_OR: "||" >
| < SC_AND: "&&" >
| < INCR: "++" >
| < DECR: "--" >
| < PLUS: "+" >
| < MINUS: "-" >
| < STAR: "*" >
| < SLASH: "/" >
| < BIT_AND: "&" >
| < BIT_OR: "|" >
| < XOR: "^" >
| < REM: "%" >
| < LSHIFT: "<<" >
| < RSIGNEDSHIFT: ">>" >
| < RUNSIGNEDSHIFT: ">>>" >
| < PLUSASSIGN: "+=" >
| < MINUSASSIGN: "-=" >
| < STARASSIGN: "*=" >
| < SLASHASSIGN: "/=" >
| < ANDASSIGN: "&=" >
| < ORASSIGN: "|=" >
| < XORASSIGN: "^=" >
| < REMASSIGN: "%=" >
| < LSHIFTASSIGN: "<<=" >
| < RSIGNEDSHIFTASSIGN: ">>=" >
| < RUNSIGNEDSHIFTASSIGN: ">>>=" >
| < ELLIPSIS: "..." >
}


/*************  ACTUAL GRAMMAR NOW *******/
// the whole file is a compilation unit
CompilationUnit CompilationUnit(String filename) :
{
    ArrayList<ImportNode> imports = new ArrayList<ImportNode>();
    ArrayList<TypeDecNode> body = new ArrayList<TypeDecNode>();
    NameNode packageNode = null;
    ImportNode temp;
    TypeDecNode temp2;
}
{
    [ packageNode=PackageDeclaration() ]
    ( temp=ImportDeclaration() { imports.add(temp); } )*
    ( temp2=TypeDeclaration() { body.add(temp2); })*
    <EOF>
    {
        return new CompilationUnit(fileName, token.beginLine, packageNode, imports, body);
    }
}

NameNode PackageDeclaration() :
{
    NameNode n;
}
{
    "package" n=Name() ";"
    {
        return n;
    }
}

ImportNode ImportDeclaration() :
{
    NameNode name;
    boolean isAll = false;
}
{
    <IMPORT> name=Name()
    [ <DOT> <STAR> { isAll = true; } ]
    <SEMICOLON>
    {
        return new ImportNode(name, isAll);
    }
}

TypeDecNode TypeDeclaration() :
{
    TypeDecNode t;
}
{
    LOOKAHEAD( ( "abstract" | "final" | "public" )* "class" )
    t=ClassDeclaration()
    { return t; }

|   LOOKAHEAD( ( "abstract" | "public")* "interface" )
    t=InterfaceDeclaration()
    { return t; }

|   t=EnumDeclaration()
    { return t; }

|   <SEMICOLON>
    { return new NoOp(fileName, token.beginLine); }
}


/*
 * Declaration syntax follows.
 */

ClassNode ClassDeclaration() :
{
    Token t;
    ClassBodyNode b;

    ArrayList<ClassBodyNode> body = new ArrayList<ClassBodyNode>();
    ArrayList<NameNode> typeParams = null;
    boolean isAbstract = false;
    boolean isFinal = false;
    boolean isPublic = false;
    NameNode superClass = null;
    ArrayList<NameNode> interfaces = null;
}
{
    ( <ABSTRACT> { isAbstract=true; }
        | <FINAL> { isFinal=true; }
        | <PUBLIC> { isPublic=true; } )*
    <CLASS> t=<IDENTIFIER> [ typeParams=ClassTypeParameters() ]
    [ <EXTENDS> superClass=Name() ]
    [ <IMPLEMENTS> interfaces=NameList() ]
    <LBRACE> ( b=ClassBodyDeclaration() { body.add(b); } )* <RBRACE>
    {
        return new ClassNode(fileName, token.beginLine, isAbstract, isFinal, isPublic, t.image, typeParams,
            superClass, interfaces, body);
    }
}

ArrayList<NameNode> ClassTypeParameters() :
{
    ArrayList<NameNode> n = new ArrayList<NameNode>();
    NameNode temp;
}
{
    <LT> temp=Name() { n.add(temp); }
    ( <COMMA> temp=Name() { n.add(temp); })* <GT>
    {
        return n;
    }
}

ClassBodyNode ClassBodyDeclaration() :
{
    ClassBodyNode c = new ClassBodyNode();
}
{
    LOOKAHEAD(2)
    c.staticInit=StaticInitializer()
    {
        return c;
    }
|
    LOOKAHEAD( [ "public" | "protected" | "private" ] Name() "(" )
    c.constructor=ConstructorDeclaration()
    {
        return c;
    }
|
    LOOKAHEAD( MethodDeclarationLookahead() )
    c.method=MethodDeclaration()
    {
        return c;
    }
|
    c.field=FieldDeclaration()
    {
        return c;
    }
}

// This production is to determine lookahead only.
void MethodDeclarationLookahead() :
{}
{
  ( "public" | "protected" | "private" | "static" | "abstract" | "final" | "native" | "synchronized" )*
  ResultType() <IDENTIFIER> "("
}

InterfaceNode InterfaceDeclaration() :
{
    // temporaries
    ClassBodyNode b;
    Token t;

    // fields of created object
    ArrayList<NameNode> extendsList = null;
    String name;
    ArrayList<ClassBodyNode> body = new ArrayList<ClassBodyNode>();
}
{
    ( <ABSTRACT> | <PUBLIC> )* // all interfaces are public and abstract by default.
    <INTERFACE> t=<IDENTIFIER> { name = t.image; }
    [ <EXTENDS> extendsList=NameList() ]
    "{" ( b=InterfaceMemberDeclaration() { body.add(b); } )* "}"
    {
        return new InterfaceNode(fileName, token.beginLine, name, extendsList, body);
    }
}

ClassBodyNode InterfaceMemberDeclaration() :
{
    ClassBodyNode c = new ClassBodyNode();
}
{
    LOOKAHEAD( MethodDeclarationLookahead() )
    c.method=MethodDeclaration()
    {
        c.method.isPublic = true;
        return c;
    }
|
    c.field=FieldDeclaration()
    {
        return c;
    }
}

EnumNode EnumDeclaration() :
{
    ArrayList<String> body;
    String name;
    Token t;
}
{
    (<PUBLIC>)* <ENUM> t=<IDENTIFIER> {name = t.image; }
    <LBRACE> body=EnumBody() <RBRACE>
    {
        return new EnumNode(fileName, token.beginLine, name, body);
    }
}

ArrayList<String> EnumBody() :
{
    ArrayList<String> a = new ArrayList<String>();
    Token t;
}
{ // the constants that could be the values of the ENUM
    t=<IDENTIFIER> { a.add(t.image); }
    ("," t=<IDENTIFIER> { a.add(t.image); })*
    {
        return a;
    }
}

FieldDeclarationNode FieldDeclaration() :
{
    FieldDeclarationNode f = new FieldDeclarationNode(fileName, token.beginLine);
    VariableDecNode v;
}
{
    ( <PUBLIC> { f.isPublic=true; } | <PROTECTED> { f.isProtected=true; }
    | <PRIVATE> { f.isPrivate=true; } | <STATIC> { f.isStatic=true; }
    | <FINAL> { f.isFinal=true; } | <TRANSIENT> { f.isTransient=true; }
    | <VOLATILE> { f.isVolatile=true; } )*
    f.type=Type() v=VariableDeclarator() { f.variables.add(v); }
    ( "," v=VariableDeclarator() { f.variables.add(v); } )* ";"
    {
        return f;
    }
}

VariableDecNode VariableDeclarator() :
{
    VariableDecNode v = new VariableDecNode(fileName, token.beginLine);
}
{
    v.id=VariableDeclaratorId() [ "=" v.init=VariableInitializer() ]
    {
        return v;
    }
}

VariableIdNode VariableDeclaratorId() :
{
    VariableIdNode v = new VariableIdNode(fileName, token.beginLine);
    Token t;
}
{
    t=<IDENTIFIER>{ v.name=t.image; }
    ( <LBRACKET> <RBRACKET> {v.numDimensions++; } )*
    {
        return v;
    }
}

VariableInitializerNode VariableInitializer() :
{
    VariableInitializerNode n = new VariableInitializerNode(fileName, token.beginLine);
    VariableInitializerNode temp;
}
{
    <LBRACE> [ temp=VariableInitializer() { n.nextLevel.add(temp); }
    ( LOOKAHEAD(2) <COMMA> temp=VariableInitializer() { n.nextLevel.add(temp); } )* ] [ "," ] <RBRACE>
    {
        return n;
    }
|
    n.e=Expression()
    {
        return n;
    }
}

MethodNode MethodDeclaration() :
{
    boolean isPublic, isProtected, isPrivate, isStatic, isAbstract, isFinal, isNative, isSynchronized;
    isPublic = isProtected = isPrivate = isStatic = isAbstract = isFinal = isNative = isSynchronized = false;
    Types returnType;
    String name;
    ArrayList<ParamNode> params;
    ArrayList<NameNode> throwsList = null;
    BlockNode code = null;

    Token t;
}
{
    ( <PUBLIC> { isPublic=true;} | <PROTECTED> { isProtected=true; }
    | <PRIVATE> { isPrivate=true; } | <STATIC> { isStatic=true; }
    | <ABSTRACT> { isAbstract=true; } | <FINAL> { isFinal=true; }
    | <NATIVE> { isNative=true; } | <SYNCHRONIZED> { isSynchronized=true; } )*
    returnType=ResultType()
    t=<IDENTIFIER> { name = t.image; }
    params=FormalParameters()
    // array parts after the parameters
    ( <LBRACKET> <RBRACKET> { returnType = Types.arrayOf(returnType); } )*

    [ <THROWS> throwsList=NameList() ]
    ( code=Block() | ";" )
    {
        return new MethodNode(fileName, token.beginLine,
            isPublic, isProtected, isPrivate, isStatic, isAbstract, isFinal, isNative, isSynchronized,
            returnType, name, params, throwsList, code);
    }
}

ArrayList<ParamNode> FormalParameters() :
{
    ArrayList<ParamNode> p = new ArrayList<ParamNode>();
    ParamNode temp;
}
{
    <LPAREN> [ temp=FormalParameter() { p.add(temp); }
    ( <COMMA> temp=FormalParameter() { p.add(temp); })* ] <RPAREN>
    {
        return p;
    }
}

ParamNode FormalParameter() :
{
    ParamNode p = new ParamNode(fileName, token.beginLine);
}
{
    p.type=Type() [<ELLIPSIS> { p.isVarargs=true; }] p.id=VariableDeclaratorId()
    {
        return p;
    }
}

ConstructorNode ConstructorDeclaration() :
{
    ConstructorNode c = new ConstructorNode(fileName, token.beginLine);
    Token t;
    BlockStatementNode b;
}
{
    [ <PUBLIC> { c.isPublic = true; }
    | <PROTECTED> {c.isProtected = true;}
    | <PRIVATE> {c.isPrivate = true; } ]
    t=<IDENTIFIER> { c.name = t.image; }
    c.params=FormalParameters()
    [ <THROWS> c.throwsList=NameList() ]
    <LBRACE> [ LOOKAHEAD(2) ExplicitConstructorInvocation() ]
    ( b=BlockStatement() { c.code.add(b); } )* <RBRACE>
    {
        return c;
    }
}

void ExplicitConstructorInvocation() :
{}
{
  "this" Arguments() ";"
|
  "super" Arguments() ";"
}

BlockNode StaticInitializer() :
{
    BlockNode b;
}
{
  "static" b=Block() { return b; }
}


/*
 * Type, name and expression syntax follows.
 */

Types Type() :
{
    Types t;
    NameNode n;
}
{
    ( t=PrimitiveType() | n=Name() { t = Types.fromFullyQualifiedClass(n.primaryName); } )
    ( <LBRACKET> <RBRACKET>{ t = Types.arrayOf(t); } )*
    { return t; }
}

Types PrimitiveType() :
{}
{
    <BOOLEAN> { return Types.BOOLEAN; }
|   <CHAR> { return Types.CHAR; }
|   <BYTE> { return Types.BYTE; }
|   <SHORT> { return Types.SHORT; }
|   <INT> { return Types.INT; }
|   <LONG> { return Types.LONG; }
|   <FLOAT> { return Types.FLOAT; }
|   <DOUBLE> { return Types.DOUBLE; }
}

Types ResultType() :
{
    Types r;
}
{
  <VOID> { return Types.VOID; }
|
  r=Type() { return r; }
}

NameNode Name() :
/*
 * A lookahead of 2 is required below since "Name" can be followed
 * by a ".*" when used in the context of an "ImportDeclaration".
 * Also support generics, making this require even more lookahead
 */
{
    Token t;
    String name;
    ArrayList<GenericNode> generics = null;
}
{
    // need to make this so that NameNode isn't recursively defined, so that
    //  we can make another method for the bounds, since you can have a list
    //  of them using super and extends, list is separated with '&'
    t=<IDENTIFIER> { name = t.image; }
    ( LOOKAHEAD(2) "." t=<IDENTIFIER> {
        name += "." + t.image;
    } )*
    ( LOOKAHEAD(<LT> GenericList() <GT>) <LT> generics=GenericList() <GT>)?
    {
        return new NameNode(fileName, token.beginLine, name, generics);
    }
}

ArrayList<GenericNode> GenericList() :
{
    ArrayList<GenericNode> n = new ArrayList<GenericNode>();
    GenericNode temp;
}
{
    temp=Generic(){ n.add(temp); }
    ( <COMMA> temp=Generic() { n.add(temp); } )*
    {
        return n;
    }
}

GenericNode Generic():
{
    GenericNode g = new GenericNode(fileName, token.beginLine);
    NameNode temp;
}
{
    (( g.extends1=Name() [<EXTENDS> g.extends2=Name()] )
    |
    ( <HOOK> (<EXTENDS> g.hookExtends=BoundList()) | (<SUPER> g.hookSuper=BoundList()) ))
    {
        return g;
    }
}

ArrayList<NameNode> BoundList() :
{
    ArrayList<NameNode> n = new ArrayList<NameNode>();
    NameNode temp;
}
{
    temp=Name(){ n.add(temp); }
    ( <BIT_AND> temp=Name() { n.add(temp); } )*
    {
        return n;
    }
}


ArrayList<NameNode> NameList() :
{
    ArrayList<NameNode> n = new ArrayList<NameNode>();
    NameNode temp;
}
{
    temp=Name(){ n.add(temp); }
    ( <COMMA> temp=Name() { n.add(temp); } )*
    {
        return n;
    }
}


/*
 * Expression syntax follows.
 */

Expression Expression() :
{
    Expression temp;
}
{
    LOOKAHEAD( PrimaryExpression() AssignmentOperator() )
    temp=Assignment() { return temp; }
|
    temp=ConditionalExpression() { return temp; }
}

AssignmentNode Assignment() :
{
    Expression left;
    BinaryOperation type;
    Expression right;
}
{
    left = PrimaryExpression()
    type = AssignmentOperator()
    right = Expression()
    {
        return new AssignmentNode(fileName, token.beginLine, left, right, type);
    }
}

/** Returns null if the assignment is just a regular =, otherwise the binary operation constant */
BinaryOperation AssignmentOperator() :
{}
{
    "=" { return null; }
|   "*=" { return BinaryOperation.TIMES; }
|   "/=" { return BinaryOperation.DIVIDE; }
|   "%=" { return BinaryOperation.MOD; }
|   "+=" { return BinaryOperation.ADD; }
|   "-=" { return BinaryOperation.SUBTRACT; }
|   "<<=" { return BinaryOperation.LEFT_SHIFT; }
|   ">>=" { return BinaryOperation.RIGHT_SHIFT_SIGN; }
|   ">>>=" { return BinaryOperation.RIGHT_SHIFT_UNSIGNED; }
|   "&=" { return BinaryOperation.AND; }
|   "^=" { return BinaryOperation.XOR; }
|   "|=" { return BinaryOperation.OR; }
}

/* return an Expression since we don't know if it's a
*  ConditionalExpression or the next level down */
Expression ConditionalExpression() :
{
    Expression condition;
    Expression truePart;
    Expression falsePart;
}
{
    condition=ConditionalOrExpression()
    [ "?" truePart=Expression() ":" falsePart=ConditionalExpression()
      { return new ConditionalExpressionNode(fileName, token.beginLine, condition, truePart, falsePart); }
    ]
    {
        return condition;
    }
}

Expression ConditionalOrExpression() :
{
    /* Build up a binary tree */
    Expression one;
    Expression two;
}
{
    one=ConditionalAndExpression()
    ( "||" two=ConditionalAndExpression()
    {
        ConditionalOrExpressionNode e = new ConditionalOrExpressionNode(fileName, token.beginLine, one, two);
        one = e;
    } )*
    {
        return one;
    }
}

Expression ConditionalAndExpression() :
{
    /* Build up a binary tree */
    Expression one;
    Expression two;
}
{
    one=InclusiveOrExpression()
    ( "&&" two=InclusiveOrExpression()
    {
        ConditionalAndExpressionNode e = new ConditionalAndExpressionNode(fileName, token.beginLine, one, two);
        one = e;
    } )*
    {
        return one;
    }
}

Expression InclusiveOrExpression() :
{
    Expression one;
    Expression two;
}
{
    one=ExclusiveOrExpression()
    ( "|" two=ExclusiveOrExpression()
    {
        BinaryExpressionNode node = new BinaryExpressionNode(fileName, token.beginLine, one, two, BinaryOperation.OR);
        one = node;
    } )*
    {
        return one;
    }
}

Expression ExclusiveOrExpression() :
{
    Expression one;
    Expression two;
}
{
    // same -- transform into the binary tree of ^ operations, if there are any
    one=AndExpression()
    ( "^" two=AndExpression()
    {
        BinaryExpressionNode node = new BinaryExpressionNode(fileName, token.beginLine, one, two, BinaryOperation.XOR);
        one = node;
    } )*
    {
        return one;
    }
}

Expression AndExpression() :
{
    Expression one;
    Expression two;
}
{
    // transform the list into the binary tree of & operations, if there are any
    one=EqualityExpression()
    ( "&" two=EqualityExpression()
    {
        BinaryExpressionNode node = new BinaryExpressionNode(fileName, token.beginLine, one, two, BinaryOperation.AND);
        one = node;
    } )*
    {
        return one;
    }
}

Expression EqualityExpression() :
{
    // this one is different than the other ones in that instead
    //  of keeping an ArrayList, we convert it into a binary tree
    //  the first two parsed become the leaf nodes

    Expression one;
    Expression two;
    ConditionCode code;
}
{
    one=InstanceOfExpression()
    ( ( "==" { code = ConditionCode.EQUAL; }
      | "!=" { code = ConditionCode.NOT_EQUAL; })
    two=InstanceOfExpression()
    {
        one = new RelationalExpressionNode(fileName, token.beginLine, one, two, code);
    })*
    {
        // one is built up, or just the expression
        return one;
    }
}

Expression InstanceOfExpression() :
{
    Expression temp;
    Types t;
}
{
    temp=RelationalExpression()
    [ "instanceof" t=Type() {
        return new InstanceOfExpressionNode(fileName, token.beginLine, temp, t);
    } ]
    {
        // simple expression instead
        return temp;
    }
}

Expression RelationalExpression() :
{
    // this one is different than the other ones in that instead
    //  of keeping an ArrayList, we convert it into a binary tree
    //  the first two parsed become the leaf nodes

    Expression one;
    Expression two;
    ConditionCode type;
}
{
    one=ShiftExpression()
    (
        ( "<" { type = ConditionCode.LESS; }
        | ">" { type = ConditionCode.GREATER; }
        | "<=" { type = ConditionCode.LESS_EQUAL;}
        | ">=" { type = ConditionCode.GREATER_EQUAL; })
        two=ShiftExpression()
        {
            one = new RelationalExpressionNode(fileName, token.beginLine, one, two, type);
        }
    )*
    {
        return one;
    }
}

Expression ShiftExpression() :
{
    Expression one;
    Expression two;
    BinaryOperation type;
}
{
    one=AdditiveExpression()
    ( ( "<<" { type = BinaryOperation.LEFT_SHIFT; }
    |   ">>" { type = BinaryOperation.RIGHT_SHIFT_SIGN; }
    |   ">>>" { type = BinaryOperation.RIGHT_SHIFT_UNSIGNED; })
    two=AdditiveExpression()
    {
        BinaryExpressionNode node = new BinaryExpressionNode(fileName, token.beginLine, one, two, type);
        one = node;
    })*
    {
        return one;
    }
}

Expression AdditiveExpression() :
{
    Expression one;
    Expression two;
    BinaryOperation type;
}
{
    one=MultiplicativeExpression()
    ( ( "+" { type = BinaryOperation.ADD; }
    |   "-" { type = BinaryOperation.SUBTRACT; })
    two=MultiplicativeExpression()
    {
        BinaryExpressionNode node = new BinaryExpressionNode(fileName, token.beginLine, one, two, type);
        one = node;
    })*
    {
        return one;
    }
}

Expression MultiplicativeExpression() :
{
    Expression one;
    Expression two;
    BinaryOperation type;
}
{
    one=UnaryExpression()
    ( ( "*" { type = BinaryOperation.TIMES; }
    |   "/" { type = BinaryOperation.DIVIDE; }
    |   "%" { type = BinaryOperation.MOD; })
    two=UnaryExpression()
    {
        BinaryExpressionNode node = new BinaryExpressionNode(fileName, token.beginLine, one, two, type);
        one = node;
    })*
    {
        return one;
    }
}

Expression UnaryExpression() :
{
    Expression e;
    boolean temp = false;
}
{
    ( "+" | "-" { temp = true; } )
    e=UnaryExpression()
    {
        if (temp) { // unary -
            return new UnaryMinusExpressionNode(fileName, token.beginLine, e);
        } else { // unary +, no meaning
            return e;
        }
    }
|
    e=PreIncrementExpression()
    {
        return e;
    }
|
    e=PreDecrementExpression()
    {
        return e;
    }
|
    e=UnaryExpressionNotPlusMinus()
    {
        return e;
    }
}

PreIncrementExpressionNode PreIncrementExpression() :
{
    PreIncrementExpressionNode e = new PreIncrementExpressionNode(fileName, token.beginLine);
}
{
    "++" e.expr=PrimaryExpression()
    {
        return e;
    }
}

PreDecrementExpressionNode PreDecrementExpression() :
{
    PreDecrementExpressionNode e = new PreDecrementExpressionNode(fileName, token.beginLine);
}
{
    "--" e.expr=PrimaryExpression()
    {
        return e;
    }
}

Expression UnaryExpressionNotPlusMinus() :
{
    Expression e;
    int type;
}
{
    ( "~" { type = 0;}
    | "!" { type = 1; })
    e=UnaryExpression()
    {
        if (type == 0) {
            return new BitwiseNotExpressionNode(fileName, token.beginLine, e);
        } else {
            return new LogicalNotExpressionNode(fileName, token.beginLine, e);
        }
    }
|
    LOOKAHEAD( CastLookahead() )
    e=CastExpression()
    {
        return e; // can just return these, those rules do the logic
    }
|
    e=PostfixExpression()
    {
        return e;
    }
}

// This production is to determine lookahead only.  The LOOKAHEAD specifications
// below are not used, but they are there just to indicate that we know about
// this.
void CastLookahead() :
{}
{
  LOOKAHEAD(2)
  "(" PrimitiveType()
|
  LOOKAHEAD("(" Name() "[")
  "(" Name() "[" "]"
|
  "(" Name() ")" ( "~" | "!" | "(" | <IDENTIFIER> | "this" | "super" | "new" | Literal() )
}

Expression PostfixExpression() :
{
    Expression e;
    int type = 0;
}
{
    e=PrimaryExpression()
    [ "++" { type = 1;} | "--" { type = 2;}]
    {
        if (type == 0) {
            return e;
        } else if (type == 1) {
            PostIncrementExpressionNode p = new PostIncrementExpressionNode(fileName, token.beginLine);
            p.expr = e;
            return p;
        } else {
            PostDecrementExpressionNode p = new PostDecrementExpressionNode(fileName, token.beginLine);
            p.expr = e;
            return p;
        }
    }
}

Expression CastExpression() :
{
    Types type;
    Expression expr;

    NameNode t;
}
{
    LOOKAHEAD(2)
    "(" type=PrimitiveType()
    ( "[" "]" { type = Types.arrayOf(type); } )* ")"
    expr=UnaryExpression()
    {
        return new CastExpressionNode(fileName, token.beginLine, type, expr);
    }
|
    "(" t=Name() { type = Types.fromFullyQualifiedClass(t.primaryName); }
    ( "[" "]" { type = Types.arrayOf(type); })* ")"
    expr=UnaryExpressionNotPlusMinus()
    {
        return new CastExpressionNode(fileName, token.beginLine, type, expr);
    }
}

Expression PrimaryExpression() :
{
    // temporary expression used in combinations
    Expression temp;
    NameNode tempName;
    ArrayList<Expression> tempList;
    Token t;


    // if the primary expression part is just 'this', which is just a qualifier for the
    //   current object; distinguishes the field or method call as of the current object.
    boolean primaryThis = false;

    // super.identifier.  Could be either super.thing(), or super.thing. The arguments later could be the call
    String superIdentifier = null;


    // the final result
    //   initially could be a primitive literal, String literal, local variable, class name, field name
    //   the suffixes will build up this value
    Expression builtUp = null;

    // field name parsed in last suffix iteration -- might be the method name, so we have to deal with
    //   it in the next iteration
    // could also be a parameter or the local variable
    String identifier = null;
}
{
    // the primary prefix -- literal, name, this, super, parentheses(expression), allocation expression.
    (
        builtUp = Literal()
    |
        t=<IDENTIFIER>
        {
            // keep it for this iteration, use in the next one.
            identifier = t.image;
        }
    |
        "this" { primaryThis = true; }
    |
        "super" "." t=<IDENTIFIER> { superIdentifier = t.image; }
    |
        // parentheses are for specifying tree order, so don't need them in the result
        //  also we can't have anything after these expressions be part of the primary expression.
        "(" temp=Expression() ")" { return temp; }
    |
        // new Something(), or new type[]...
        builtUp=AllocationExpression()
    )

    // primary suffixes -- 0...many
    //  can be either a [expression], .identifier, or (arg1, arg...)
    (
        // requires the builtUp to not be null, meaning we have a valid object in builtUp
        "[" temp=Expression() "]"
            {
                if (builtUp == null) {
                    if (primaryThis) {
                        throw new ParseException("Can't array index 'this' qualifier.");
                    } else if (superIdentifier != null){
                        // represents super.identifier[expression]
                        builtUp = new SuperFieldExpressionNode(superIdentifier, fileName, token.beginLine);
                    }
                }
                
                if (builtUp == null) {
                    // identifier is not null, build up from that
                    builtUp = new UnknownIdentifierNode(identifier, fileName, token.beginLine);
                    identifier = null; // clear for the next iteration
                } else if (identifier != null) {
                    // here builtUp is definitely not null
                    // this means it's a true field name
                    builtUp = new FieldExpressionNode(builtUp, identifier, fileName, token.beginLine);
                    identifier = null; // clear for next iteration
                }

                // builtUp = the array indexing operator of builtUp
                builtUp = new ArrayIndexExpressionNode(builtUp, temp, fileName, token.beginLine);
            }
        |
            "." t=<IDENTIFIER>
            {
                if (builtUp == null) {
                    if (primaryThis) {
                        // this.identifier
                        builtUp = new ThisExpressionNode(fileName, token.beginLine);
                    } else if (superIdentifier != null){
                        // super.identifier.identifier
                        builtUp = new SuperFieldExpressionNode(identifier, fileName, token.beginLine);
                    } else {
                        // don't know what the specific type is
                        builtUp = new UnknownIdentifierNode(identifier, fileName, token.beginLine);
                    }
                    identifier = t.image;
                } else if (identifier != null){
                    // builtUp is already something, set the field name
                    builtUp = new FieldExpressionNode(builtUp, identifier, fileName, token.beginLine);
                    identifier = t.image;
                } else {
                    // identifier == null, there's not anything extra we need to build up
                    identifier = t.image;
                }

            }
        |
            tempList=Arguments() // just grab the args
            {
                if (identifier == null) {
                    throw new ParseException("Method call specified without a name.");
                }
                // builtUp = a function call of builtUp.identifier(arguments)
                builtUp = new MethodCallNode(builtUp, identifier, tempList, fileName, token.beginLine);

                // consumes the field name that was previously set
                identifier = null;
            }
    )*

    {
        // potential the last result was a .name, so we have to add that in.
        if (identifier != null) {
            if (builtUp == null) {
                builtUp = new UnknownIdentifierNode(identifier, fileName, token.beginLine);
            } else {
                builtUp = new FieldExpressionNode(builtUp, identifier, fileName, token.beginLine);
            }
        }
        return builtUp;
    }
}


LiteralExpressionNode Literal() :
{
    Token t;
    LiteralExpressionNode n;
}
{
    (n=NumericalLiteral() { return n; })
|
    (t=<CHARACTER_LITERAL>
|   t=<STRING_LITERAL>
|   t=<TRUE>
|   t=<FALSE>
|   t=<NULL>)
    {
        return new LiteralExpressionNode(fileName, token.beginLine, t.image);
    }
}

/* These numerical literals are implemented in order of how they are in the Java Language Specification */

LiteralExpressionNode NumericalLiteral():
{ NumericalLiteralNode n; }
{
    n=IntegerLiteral() { return n; }
/*|
    n=FloatingPointLiteral() { return n; }
    When floating point literals are implemented,
    https://docs.oracle.com/javase/specs/jls/se11/html/jls-3.html#jls-FloatingPointLiteral
    */
}

IntegerLiteralNode IntegerLiteral(): // either int or long
{
    long value;
}
{
    (
        value = DecimalLiteral()
    |   value = HexIntegerLiteral()
    |   value = OctalIntegerLiteral()
    |   value = BinaryIntegerLiteral()
    )
    ( ["l" "L"] { return new LongLiteralNode(value); })?
    { return new IntegerLiteralNode(value); }
}

long DecimalLiteral():
{
    long value = 0;
    long temp;
    String temp2;
}
{
    "0" { return 0; }
|   temp = NonZeroDigit() { value *= 10;  value += temp; }
    ( temp=Digits() { temp2 = "" + value + temp; value = Long.parseLong(temp2); })?
    {
        return value;
    }
|
    temp = NonZeroDigit() { value *= 10; value += temp; }
    Underscores()
    temp = Digits() { temp2 = "" + value + temp; value = Long.parseLong(temp2); }
    {
        return value;
    }
}

long NonZeroDigit():{}
{
    "1" { return 1; }
|   "2" { return 2; }
|   "3" { return 3; }
|   "4" { return 4; }
|   "5" { return 5; }
|   "6" { return 6; }
|   "7" { return 7; }
|   "8" { return 8; }
|   "9" { return 9; }
}

// returns the numerical value of the digits
long Digits():
{
    long result;
    long temp;
    Long temp2;
    String concat;
}
{
    result=Digit()
    (
        (temp2=DigitsAndUnderscores() {
            // concatenate result if not null
            if (temp2 != null) {
                concat = "" + result + temp2;
                result = Long.parseLong(concat);
            }
        })?
        temp=Digit() { /* concatenate result */ result *= 10; result += temp; }
    )?
}

long Digit():
{
    long temp;
}
{
    "0" { return 0; }
|   temp=NonZeroDigit() { return temp; }
}

// returns the value, or null if it's just a bunch of underscores
Long DigitsAndUnderscores():
{
    Long result = null;
    Long temp;
    String concat;
}
{
    (
        temp=DigitOrUnderscore()
        {
            if (result == null) {
                result = temp;
            } else if (temp != null) {
                // concatenate
                concat = "" + result + temp;
                result = Long.parseLong(concat);
            }
            // no change otherwise
        }
    )+
    {
        return result;
    }
}

// will return null if it's an underscore
Long DigitOrUnderscore():
{
    long temp;
}
{
    temp=Digit() { return temp; }
|
    "_" { return null; }
}

// consumes underscore tokens
void Underscores():{}
{
    "_" ("_")*
}

long HexIntegerLiteral():
{
    long value;
}
{
    "0" ["X" "x"] value = HexDigits() { return value; }
}

// similar to DecimalDigits, but multiply by 16
long HexDigits():
{
    long result;
    long temp;
    Long temp2;
    String concat;
}
{
    result=HexDigit()
    (
        (temp2=HexDigitsAndUnderscores() {
            // concatenate result if not null
            if (temp2 != null) {
                concat = "" + result + temp2;
                result = Long.parseLong(concat, 16);
            }
        })?
        temp=HexDigit() { /* concatenate result */ result *= 16; result += temp; }
    )?
}

// comsumes a hex digit and returns its value.
long HexDigit():
{
    long value;
}
{
    value=Digit() { return value; }
|   ["a" "A"] { return 10; }
|   ["b" "B"] { return 11; }
|   ["c" "C"] { return 12; }
|   ["d" "D"] { return 13; }
|   ["e" "E"] { return 14; }
|   ["f" "F"] { return 15; }
}

Long HexDigitsAndUnderscores():
{
    Long result = null;
    Long temp;
    String concat;
}
{
    (
        temp=HexDigitOrUnderscore()
        {
            if (result == null) {
                result = temp;
            } else if (temp != null) {
                // concatenate
                concat = "" + result + temp;
                result = Long.parseLong(concat, 16);
            }
            // no change otherwise
        }
    )+
    {
        return result;
    }
}

Long HexDigitOrUnderscore():
{
    long value;
}
{
    value=HexDigit() { return value; }
|
    "_" { return null; }
}

// similar pattern as the hex ones
long OctalIntegerLiteral():
{
    long value;
}
{
    "0" (Underscores())? value=OctalDigits() { return value; }
}

long OctalDigits():
{
    long result;
    long temp;
    Long temp2;
    String concat;
}
{
    result=OctalDigit()
    (
        (temp2=OctalDigitsAndUnderscores() {
            // concatenate result if not null
            if (temp2 != null) {
                concat = "" + result + temp2;
                result = Long.parseLong(concat, 8);
            }
        })?
        temp=HexDigit() { /* concatenate result */ result *= 8; result += temp; }
    )?
}

// comsumes a octal digit and returns its value.
long OctalDigit():{}
{
    "0" { return 0; }
|   "1" { return 1; }
|   "2" { return 2; }
|   "3" { return 3; }
|   "4" { return 4; }
|   "5" { return 5; }
|   "6" { return 6; }
|   "7" { return 7; }
|   "8" { return 8; }
|   "9" { return 9; }
}

Long OctalDigitsAndUnderscores():
{
    Long result = null;
    Long temp;
    String concat;
}
{
    (
        temp=OctalDigitOrUnderscore()
        {
            if (result == null) {
                result = temp;
            } else if (temp != null) {
                // concatenate
                concat = "" + result + temp;
                result = Long.parseLong(concat, 8);
            }
            // no change otherwise
        }
    )+
    {
        return result;
    }
}


Long OctalDigitOrUnderscore():
{
    long value;
}
{
    value=OctalDigit() { return value; }
|
    "_" { return null; }
}

// also similar pattern
long BinaryIntegerLiteral():
{
    long value;
}
{
    "0" ["b" "B"] (Underscores())? value=BinaryDigits() { return value; }
}

long BinaryDigits():
{
    long result;
    long temp;
    Long temp2;
    String concat;
}
{
    result=BinaryDigit()
    (
        (temp2=BinaryDigitsAndUnderscores() {
            // concatenate result if not null
            if (temp2 != null) {
                concat = "" + result + temp2;
                result = Long.parseLong(concat, 2);
            }
        })?
        temp=BinaryDigit() { /* concatenate result */ result *= 8; result += temp; }
    )?
}

// comsumes a binary digit and returns its value.
long BinaryDigit():{}
{
    "0" { return 0; }
|   "1" { return 1; }
}

Long BinaryDigitsAndUnderscores():
{
    Long result = null;
    Long temp;
    String concat;
}
{
    (
        temp=BinaryDigitOrUnderscore()
        {
            if (result == null) {
                result = temp;
            } else if (temp != null) {
                // concatenate
                concat = "" + result + temp;
                result = Long.parseLong(concat, 2);
            }
            // no change otherwise
        }
    )+
    {
        return result;
    }
}


Long BinaryDigitOrUnderscore():
{
    long value;
}
{
    value=BinaryDigit() { return value; }
|
    "_" { return null; }
}


ArrayList<Expression> Arguments() :
{
    ArrayList<Expression> expressions = new ArrayList<Expression>();
}
{
    "(" [ expressions=ArgumentList() ] ")"
    {
        return expressions;
    }
}

ArrayList<Expression> ArgumentList() :
{
    ArrayList<Expression> a = new ArrayList<Expression>();
    Expression e;
}
{
    e=Expression() { a.add(e); }
    ( "," e=Expression() { a.add(e); })*
    {
        return a;
    }
}

Expression AllocationExpression() :
{
    Expression temp;
    ArrayList<Expression> list;
    Types p;
    NameNode n;
}
{
    LOOKAHEAD(2)
    "new" p=PrimitiveType() list=ArrayDimensions()
    {
        ArrayAllocationNode a = new ArrayAllocationNode(fileName, token.beginLine);
        a.type = p;
        a.expressions = list;
        return a;
    }
|
    "new" n=Name()
    ( list=Arguments()
        {
            ConstructorCallNode c = new ConstructorCallNode(fileName, token.beginLine);
            c.name = n;
            c.args = list;
            return c;
        }
    | list=ArrayDimensions()
    {
        ArrayAllocationNode o = new ArrayAllocationNode(fileName, token.beginLine);
        o.type = Types.fromFullyQualifiedClass(n.primaryName);
        o.expressions = list;
        return o;
    } )
}

/*
 * The second LOOKAHEAD specification below is to parse to PrimarySuffix
 * if there is an expression between the "[...]".
 */
ArrayList<Expression> ArrayDimensions() :
{
    ArrayList<Expression> e = new ArrayList<Expression>();
    Expression temp;
}
{
    ( LOOKAHEAD(2) "[" temp=Expression() { e.add(temp); }"]" )+
    ( LOOKAHEAD(2) "[" "]" { e.add(null); })*
    {
        return e;
    }
}


/*
 * Statement syntax follows.
 */

StatementNode Statement() :
{
    StatementNode s;
}
{
    LOOKAHEAD(2)
    s=LabeledStatement()
    {
        return s;
    }
|
    s=Block()
    {
        return s;
    }
|
    EmptyStatement()
    {
        return new NoOp(fileName, token.beginLine);
    }
|
    s=StatementExpression() <SEMICOLON>
    {
        return s;
    }
|
    s=SwitchStatement()
    {
        return s;
    }
|
    s=IfStatement()
    {
        return s;
    }
|
    s=WhileStatement()
    {
        return s;
    }
|
    s=DoStatement()
    {
        return s;
    }
|
    s=ForStatement()
    {
        return s;
    }
|
    s=BreakStatement()
    {
        return s;
    }
|
    s=ContinueStatement()
    {
        return s;
    }
|
    s=ReturnStatement()
    {
        return s;
    }
|
    s=ThrowStatement()
    {
        return s;
    }
|
    s=SynchronizedStatement()
    {
        return s;
    }
|
    s=TryStatement()
    {
        return s;
    }
}

LabeledStatementNode LabeledStatement() :
{
    StatementNode statement;
    Token t;
}
{
    t=<IDENTIFIER> ":" statement=Statement()
    {
        return new LabeledStatementNode(fileName, token.beginLine, t.image, statement);
    }
}

BlockNode Block() :
{
    int line = token.beginLine;
    ArrayList<BlockStatementNode> statements = new ArrayList<BlockStatementNode>();
    BlockStatementNode s;
}
{
    <LBRACE> ( s=BlockStatement()
    { statements.add(s); } )* <RBRACE>
    {
        return new BlockNode(fileName, line, statements);
    }
}

BlockStatementNode BlockStatement() :
{
    BlockStatementNode b;
}
{
    LOOKAHEAD(Type() <IDENTIFIER>)
    b=LocalVariableDeclaration() <SEMICOLON>
    {
        return b;
    }
|
    b=Statement()
    {
        return b;
    }
}

LocalVariableDecNode LocalVariableDeclaration() :
{
    Types type;
    ArrayList<VariableDecNode> declarations = new ArrayList<VariableDecNode>();
    VariableDecNode temp;
}
{
    type=Type() temp=VariableDeclarator() { declarations.add(temp); }
    ( <COMMA> temp=VariableDeclarator() { declarations.add(temp); })*
    {
        return new LocalVariableDecNode(fileName, token.beginLine, type, declarations);
    }
}

// don't need a return type here
void EmptyStatement() :
{}
{
    <SEMICOLON>
}

StatementExprNode StatementExpression() :
/*
 * The last expansion of this production accepts more than the legal
 * Java expansions for StatementExpression.
 */
{
    StatementExprNode e;
}
{
    e=PreIncrementExpression()
    {
        return e;
    }
|
    e=PreDecrementExpression()
    {
        return e;
    }
|
    LOOKAHEAD( PrimaryExpression() AssignmentOperator() )
    e=Assignment()
    {
        return e;
    }
|
    e=PostfixExpression()
    {
        return e;
    }
}

SwitchStatementNode SwitchStatement() :
{
    Expression expression; // switch(expression)
    ArrayList<SwitchStatementPart> parts = new ArrayList<SwitchStatementPart>(); // label: (Statements)*

    SwitchLabelNode tempLabel;
    ArrayList<BlockStatementNode> statements = new ArrayList<BlockStatementNode>();
    BlockStatementNode tempExpression;
}
{
    <SWITCH> <LPAREN> expression=Expression() <RPAREN> <LBRACE>
        ( tempLabel=SwitchLabel()
            ( tempExpression=BlockStatement() { statements.add(tempExpression); } )*
            {
                parts.add(new SwitchStatementPart(fileName, token.beginLine, tempLabel, statements));
            }
        )*
    <RBRACE>
    {
        return new SwitchStatementNode(fileName, token.beginLine, expression, parts);
    }
}

SwitchLabelNode SwitchLabel() :
{
    Expression expr;
}
{
  <CASE> expr=Expression() <COLON> { return new SwitchLabelNode(fileName, token.beginLine, expr); }
|
  <_DEFAULT> <COLON> { return new SwitchLabelNode(fileName, token.beginLine, null); }
}

IfStatementNode IfStatement() :
/*
 * The disambiguating algorithm of JavaCC automatically binds dangling
 * else's to the innermost if statement.  The LOOKAHEAD specification
 * is to tell JavaCC that we know what we are doing.
 * This also does handle the if .. else if ... else ...
 * as the second if is part of the last expression thing.
 */
{
    Expression expr;
    StatementNode truePart;
    StatementNode falsePart = null;
}
{
    <IF> <LPAREN> expr=Expression() <RPAREN>
    truePart=Statement() [ LOOKAHEAD(1) <ELSE> falsePart=Statement() ]
    {
        return new IfStatementNode(fileName, token.beginLine, expr, truePart, falsePart);
    }
}

WhileStatementNode WhileStatement() :
{
    Expression expression;
    StatementNode statement;
}
{
    <WHILE> <LPAREN> expression=Expression() <RPAREN>
    statement=Statement()
    {
        return new WhileStatementNode(fileName, token.beginLine, expression, statement);
    }
}

DoStatementNode DoStatement() :
{
    Expression expression;
    StatementNode statement;
}
{
    <DO> statement=Statement() <WHILE> <LPAREN>
    expression=Expression() <RPAREN> <SEMICOLON>
    {
        return new DoStatementNode(fileName, token.beginLine, statement, expression);
    }
}

ForStatementNode ForStatement() :
{
    ForInitNode init = null;
    Expression condition = null;
    StatementExprNodeList update = null;
    StatementNode block;
}
{
    <FOR> <LPAREN> [ init=ForInit() ] <SEMICOLON>
    [ condition=Expression() ] <SEMICOLON>
    [ update=ForUpdate() ] <RPAREN> block=Statement()
    {
        return new ForStatementNode(fileName, token.beginLine, init, condition, update, block);
    }
}

ForInitNode ForInit() :
{
    ForInitNode temp;
}
{
    LOOKAHEAD( Type() <IDENTIFIER> )
    temp=LocalVariableDeclaration()
    {
        return temp;
    }
|
    temp=StatementExpressionList()
    {
        return temp;
    }
}

StatementExprNodeList StatementExpressionList() :
{
    ArrayList<StatementExprNode> s = new ArrayList<StatementExprNode>();
    StatementExprNode temp;
}
{
    temp=StatementExpression() { s.add(temp); }
    ( <COMMA> temp=StatementExpression() { s.add(temp); } )*
    {
        return new StatementExprNodeList(fileName, token.beginLine, s);
    }
}

StatementExprNodeList ForUpdate() :
{
    /* This rule is just a statement expression list */
    StatementExprNodeList t;
}
{
    t=StatementExpressionList()
    {
        return t;
    }
}

BreakStatementNode BreakStatement() :
{
    Token t;
    String image = null;
}
{
    <BREAK> [ t=<IDENTIFIER> { image = t.image; }] <SEMICOLON>
    {
        return new BreakStatementNode(fileName, token.beginLine, image);
    }
}

ContinueStatementNode ContinueStatement() :
{
    Token t;
    String image = null;
}
{
    <CONTINUE> [ t=<IDENTIFIER> { image = t.image; } ] <SEMICOLON>
    {
        return new ContinueStatementNode(fileName, token.beginLine, image);
    }
}

ReturnStatementNode ReturnStatement() :
{
    Expression expression = null;
}
{
    <RETURN> [ expression=Expression() ] <SEMICOLON>
    {
        return new ReturnStatementNode(fileName, token.beginLine, expression);
    }
}

ThrowStatementNode ThrowStatement() :
{
    Expression expression;
}
{
    <THROW> expression=Expression() <SEMICOLON>
    {
        return new ThrowStatementNode(fileName, token.beginLine, expression);
    }
}

SynchronizedStatementNode SynchronizedStatement() :
{
    BlockNode block;
    Expression expression;
}
{
    <SYNCHRONIZED> <LPAREN> expression=Expression()
    <RPAREN> block=Block()
    {
        return new SynchronizedStatementNode(fileName, token.beginLine, expression, block);
    }
}

TryStatementNode TryStatement() :
/*
 * Semantic check required here to make sure that at least one
 * finally/catch is present.
 */
{
    TryStatementNode t = new TryStatementNode(fileName, token.beginLine);
    t.catchParams = new ArrayList<ParamNode>();
    t.catchBlocks = new ArrayList<BlockNode>();
    ParamNode temp;
    BlockNode temp2;
}
{
    <TRY> t.block=Block()
    ( <CATCH> <LPAREN> temp=FormalParameter() { t.catchParams.add(temp); } <RPAREN>
     temp2=Block() { t.catchBlocks.add(temp2); } )*
    [ <FINALLY> t.finallyPart=Block() ]
    {
        return t;
    }
}
